# ---------------------------------------------
# Key pair
# ---------------------------------------------
resource "aws_key_pair" "keypair" {
  key_name   = "${local.project}-keypair"
  public_key = file("./credential/${local.project}-keypair.pub")

  tags = {
    Name    = "${local.project}-keypair"
    Project = local.project
  }
}

# ---------------------------------------------
# EC2 instance
# ---------------------------------------------
resource "aws_instance" "app" {
  ami                         = data.aws_ami.grpc_sample.id
  instance_type               = var.instance_type
  subnet_id                   = aws_subnet.public_subnet_1a.id
  associate_public_ip_address = true
  vpc_security_group_ids = [
    aws_security_group.app_sg.id
  ]
  key_name = aws_key_pair.keypair.key_name

  connection {
    type        = "ssh"
    host        = self.public_ip
    user        = "ec2-user"
    private_key = file("./credential/${local.project}-keypair.pem")
  }
  provisioner "remote-exec" {
    inline = [
      "touch ~/export.sh",
      "echo '#!/bin/bash' >> ~/export.sh",
      "echo 'export DB_HOST=${aws_db_instance.mysql_standalone.address}' >> ~/export.sh",
      "echo 'export DB_PORT=${aws_db_instance.mysql_standalone.port}' >> ~/export.sh",
      "echo 'export DB_DATABASE=${aws_db_instance.mysql_standalone.db_name}' >> ~/export.sh",
      "echo 'export DB_USER=${aws_db_instance.mysql_standalone.username}' >> ~/export.sh",
      "echo 'export DB_PASSWORD=${random_string.db_password.result}' >> ~/export.sh"
    ]
  }

  tags = {
    Name    = "${local.project}-app"
    Project = local.project
    Type    = "app"
  }
}

// AMI generated by https://github.com/takahiroaoki/packer-container
data "aws_ami" "grpc_sample" {
  most_recent = true
  owners      = ["self"]

  filter {
    name   = "name"
    values = ["grpc-sample-ami-*"]
  }
}